HttpClient 

todo

ACE 
	Init(hash)
		on ok (
			set filter 
			set categoryId
		) triggers (GetTodoList)
	
	CreateCategory
		fromAppState(categoryId)
			call com.anfelisa.todo.CreateCategory
			loadingFlag loading 
		on ok triggers ( GetTodoList )
	
	GetTodoList 
		fromAppState(categoryId)
			call com.anfelisa.todo.GetAllTodos  
			loadingFlag loading 
		on ok (set todoList)
		on categoryDoesNotExist triggers (CreateCategory)
	
	NewTodoChanged (newTodo)
		on ok (set newTodo)
	
	CreateTodo (description)
		fromAppState(categoryId)
			call com.anfelisa.todo.CreateTodo
			loadingFlag loading 
		on ok (set newTodo) triggers ( GetTodoList)
		on empty
		
	EditTodo (id)
		fromAppState ( todoList )
		on ok ( set editedTodo)
	
	EditedTodoChanged (editedDescription)
		on ok ( set editedTodo.editedDescription)
	
	UpdateTodo
		fromAppState ( 
			editedTodo.id as id, 
			editedTodo.editedDescription as description
		)
			call com.anfelisa.todo.UpdateTodo
			loadingFlag loading 
		on ok (
			set editedTodo 
		) triggers (GetTodoList)
		on empty (
			set editedTodo 
		)
	
	ToggleTodo (id)
			call com.anfelisa.todo.ToggleTodo
			loadingFlag loading 
		on ok triggers (GetTodoList)
	
	ToggleAll 
		fromAppState(categoryId)
			call com.anfelisa.todo.ToggleAll 
			loadingFlag loading 
		on ok triggers (GetTodoList)
	
	DeleteTodo (id)
			call com.anfelisa.todo.DeleteTodo 
			loadingFlag loading 
		on ok triggers (GetTodoList)
	
	ClearDone
		fromAppState(categoryId) 
			call com.anfelisa.todo.ClearDone 
			loadingFlag loading 
		on ok triggers (GetTodoList)
		
	DisplayError (error)
		on ok (set error)
	
appState {
	String filter
	String newTodo
	Boolean loading
	String categoryId
	List todoList {
		String id
		String description
		Boolean done
		DateTime createdDateTime
		DateTime updatedDateTime
	}
	String error
	editedTodo {
		String editedDescription
		String id
		String description
	}
}
