HttpClient React_16.8

todo 

ACE 
	Init(hash)
		on ok (
			set filter 
			set footer.categoryId
		) triggers (GetTodoList)
	
	CreateCategory
		fromAppState(footer.categoryId)
			call com.anfelisa.todo.CreateCategory
			loadingFlag spinner.loading 
		on ok triggers ( GetTodoListWithoutCategoryCheck )
	
	GetTodoList 
		fromAppState(footer.categoryId)
			call com.anfelisa.todo.GetAllTodos  
			loadingFlag spinner.loading 
		on ok (set todos.todoList)
		on categoryDoesNotExist triggers (CreateCategory)
	
	GetTodoListWithoutCategoryCheck 
		fromAppState(footer.categoryId)
			call com.anfelisa.todo.GetAllTodos  
			loadingFlag spinner.loading 
		on ok (set todos.todoList)
	
	NewTodoChanged (newTodo)
		on ok (set newTodoInput.newTodo)
	
	NewTodoKeyPressed (charCode)
		on enter triggers ( CreateTodo )
		on notEnter
	
	CreateTodo
		fromAppState(newTodoInput.newTodo as description, footer.categoryId)
			call com.anfelisa.todo.CreateTodo
			loadingFlag spinner.loading 
		on ok (set newTodoInput.newTodo) triggers ( GetTodoList)
		on empty
		
	EditTodo (id)
		fromAppState ( todos.todoList )
		on ok ( 
			set todos.editedDescription 
			set todos.editedTodoId 
		)
	
	EditedTodoChanged (editedDescription)
		on ok ( set todos.editedDescription)
	
	EditedTodoKeyPressed (charCode)
		on enter triggers ( UpdateTodo )
		on notEnter
	
	UpdateTodo
		fromAppState ( 
			todos.editedTodoId as id, 
			todos.editedDescription as description
		)
			call com.anfelisa.todo.UpdateTodo
			loadingFlag spinner.loading 
		on ok (
			set todos.editedDescription 
			set todos.editedTodoId 
		) triggers (GetTodoList)
		on empty (
			set todos.editedDescription 
			set todos.editedTodoId 
		)
	
	ToggleTodo (id)
			call com.anfelisa.todo.ToggleTodo
			loadingFlag spinner.loading 
		on ok triggers (GetTodoList)
	
	ToggleAll 
		fromAppState(footer.categoryId)
			call com.anfelisa.todo.ToggleAll 
			loadingFlag spinner.loading 
		on ok triggers (GetTodoList)
	
	DeleteTodo (id)
			call com.anfelisa.todo.DeleteTodo 
			loadingFlag spinner.loading 
		on ok triggers (GetTodoList)
	
	ClearDone
		fromAppState(footer.categoryId) 
			call com.anfelisa.todo.ClearDone 
			loadingFlag spinner.loading 
		on ok triggers (GetTodoList)
		
	DisplayError (errorMessage)
		on ok (set error.errorMessage)












ui {
	footer {
		categoryId
	}
	filter
	newTodoInput {
		newTodo
	}
	spinner {
		loading
	}
	error {
		errorMessage
	}
	todos {
		List todoList {
			categoryId
			id
			description
			done
			createdDateTime
			updatedDateTime
		}
		editedDescription
		editedTodoId
	}
}
